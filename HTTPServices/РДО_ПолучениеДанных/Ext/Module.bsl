
Функция УниверсальныйМетод(HTTPЗапрос)
	Массив = СтрРазделить(HTTPЗапрос.ОтносительныйURL, "/"); 
	Запрос = Новый Запрос; 
	Запрос.Текст = "ВЫБРАТЬ
			|	ДополнительныеОтчетыИОбработкиКоманды.Идентификатор КАК Идентификатор,
			|	ДополнительныеОтчетыИОбработкиКоманды.Метод КАК Метод,
			|	ДополнительныеОтчетыИОбработкиКоманды.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ДополнительныеОтчетыИОбработки.Команды КАК ДополнительныеОтчетыИОбработкиКоманды
			|ГДЕ ИСТИНА
			|	И ДополнительныеОтчетыИОбработкиКоманды.Идентификатор = &Идентификатор
			|	И ДополнительныеОтчетыИОбработкиКоманды.Метод = &Метод
			|	И ДополнительныеОтчетыИОбработкиКоманды.ВариантЗапуска = ЗНАЧЕНИЕ(Перечисление.СпособыВызоваДополнительныхОбработок.ВызовHTTPСервиса)
			|";
	Запрос.Параметры.Вставить("Идентификатор", Массив[1]);
	Запрос.Параметры.Вставить("Метод", HTTPЗапрос.HTTPМетод);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Новый HTTPСервисОтвет(404);				
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("HTTPЗапрос", HTTPЗапрос);
	ПараметрыКоманды.Вставить("Метод", Выборка.Метод);
	ПараметрыКоманды.Вставить("ДополнительнаяОбработкаСсылка", Выборка.Ссылка);
	ПараметрыКоманды.Вставить("ИдентификаторКоманды", Выборка.Идентификатор);
	Возврат ДополнительныеОтчетыИОбработки.ВыполнитьКоманду(ПараметрыКоманды).HTTPОтвет;
КонецФункции
